# FunC Yemek KitabÄ±

FunC Yemek KitabÄ±'nÄ±n oluÅŸturulmasÄ±nÄ±n temel nedeni, FunC geliÅŸtiricilerinin tÃ¼m deneyimlerini bir araya getirerek gelecekteki geliÅŸtiricilerin bunu kullanabilmelerini saÄŸlamaktÄ±r!

`FunC DokÃ¼mantasyonu` ile karÅŸÄ±laÅŸtÄ±rÄ±ldÄ±ÄŸÄ±nda, bu makale her FunC geliÅŸtiricisinin akÄ±llÄ± sÃ¶zleÅŸmelerin geliÅŸtirilmesi sÄ±rasÄ±nda Ã§Ã¶zdÃ¼ÄŸÃ¼ gÃ¼nlÃ¼k gÃ¶revlere daha fazla odaklanmaktadÄ±r.

## Temeller

### Bir if ifadesi nasÄ±l yazÄ±lÄ±r

Herhangi bir olayÄ±n ilgili olup olmadÄ±ÄŸÄ±nÄ± kontrol etmek istediÄŸimizi varsayalÄ±m. Bunu yapmak iÃ§in bayrak deÄŸiÅŸkenini kullanÄ±yoruz. FunC'de `true` deÄŸerinin `-1` ve `false` deÄŸerinin `0` olduÄŸunu unutmayÄ±n.

```func
int flag = 0; ;; yanlÄ±ÅŸ

if (flag) { 
    ;; bir ÅŸey yap
}
else {
    ;; iÅŸlemi reddet
}
```

> ğŸ’¡ **Not:** KullanÄ±cÄ± `==` operatÃ¶rÃ¼ne ihtiyacÄ±mÄ±z yok, Ã§Ã¼nkÃ¼ `0` deÄŸeri `false` olduÄŸu iÃ§in diÄŸer herhangi bir deÄŸer `true` olur.

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**  
> `"If ifadesi" belgelerde`

### Bir repeat dÃ¶ngÃ¼sÃ¼ nasÄ±l yazÄ±lÄ±r

Ã–rnek olarak Ã¼slÃ¼ sayma iÅŸlemini alabiliriz.

```func
int number = 2;
int multiplier = number;
int degree = 5;

repeat(degree - 1) {
    number *= multiplier;
}
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**  
> `"Repeat dÃ¶ngÃ¼sÃ¼" belgelerde`

### Bir while dÃ¶ngÃ¼sÃ¼ nasÄ±l yazÄ±lÄ±r

Belirli bir eylemi ne sÄ±klÄ±kla gerÃ§ekleÅŸtireceÄŸimizi bilmediÄŸimizde while kullanÄ±ÅŸlÄ±dÄ±r. Ã–rneÄŸin, dÃ¶rt referansa kadar diÄŸer hÃ¼creleri depolayabilen bir `cell` alalÄ±m.

```func
cell inner_cell = begin_cell() ;; yeni bir boÅŸ oluÅŸturucu oluÅŸtur 
        .store_uint(123, 16) ;; deÄŸeri 123 ve uzunluÄŸu 16 bit olan uint depola
        .end_cell(); ;; oluÅŸturucuyu bir hÃ¼creye dÃ¶nÃ¼ÅŸtÃ¼r

cell message = begin_cell()
        .store_ref(inner_cell) ;; hÃ¼creyi referans olarak depola
        .store_ref(inner_cell)
        .end_cell();

slice msg = message.begin_parse(); ;; hÃ¼creyi dilime dÃ¶nÃ¼ÅŸtÃ¼r
while (msg.slice_refs_empty?() != -1) { ;; -1'in true olduÄŸunu hatÄ±rlatmalÄ±yÄ±z
    cell inner_cell = msg~load_ref(); ;; dilim msg'den hÃ¼creyi yÃ¼kle
    ;; bir ÅŸey yap
}
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**  
> `"While dÃ¶ngÃ¼sÃ¼" belgelerde`  
> `"Cell" belgelerde`  
> `"slice_refs_empty?()" belgelerde`  
> `"store_ref()" belgelerde`  
> `"begin_cell()" belgelerde`  
> `"end_cell()" belgelerde`  
> `"begin_parse()" belgelerde`

### Bir do until dÃ¶ngÃ¼sÃ¼ nasÄ±l yazÄ±lÄ±r

DÃ¶ngÃ¼nÃ¼n en az bir kez Ã§alÄ±ÅŸmasÄ±nÄ± gerektiÄŸinde `do until` kullanÄ±yoruz.

```func 
int flag = 0;

do {
    ;; bayrak yanlÄ±ÅŸ (0) olduÄŸu iÃ§in bile bir ÅŸey yap 
} until (flag == -1); ;; -1 true
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**  
> `"Until dÃ¶ngÃ¼sÃ¼" belgelerde`

### Dilimin boÅŸ olup olmadÄ±ÄŸÄ±nÄ± nasÄ±l belirleriz

`slice` ile Ã§alÄ±ÅŸmadan Ã¶nce, doÄŸru bir ÅŸekilde iÅŸleyebilmesi iÃ§in iÃ§inde veri olup olmadÄ±ÄŸÄ±nÄ± kontrol etmemiz gerekir. Bunu yapmak iÃ§in `slice_empty?()` kullanabiliriz, ancak en az bir `bit` veri veya bir `ref` varsa `0` (`false`) dÃ¶neceÄŸini dikkate almalÄ±yÄ±z.

```func
;; boÅŸ dilim oluÅŸturma
slice empty_slice = "";
;; `slice_empty?()` `true` dÃ¶ner, Ã§Ã¼nkÃ¼ dilimde herhangi bir `bit` veya `ref` yoktur
empty_slice.slice_empty?();

;; yalnÄ±zca bit iÃ§eren dilim oluÅŸturma
slice slice_with_bits_only = "Merhaba, dÃ¼nya!";
;; `slice_empty?()` `false` dÃ¶ner, Ã§Ã¼nkÃ¼ dilimde herhangi bir `bit` bulunur
slice_with_bits_only.slice_empty?();

;; yalnÄ±zca ref iÃ§eren dilim oluÅŸturma
slice slice_with_refs_only = begin_cell()
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_empty?()` `false` dÃ¶ner, Ã§Ã¼nkÃ¼ dilimde herhangi bir `ref` bulunur
slice_with_refs_only.slice_empty?();

;; hem bit hem de ref iÃ§eren dilim oluÅŸturma
slice slice_with_bits_and_refs = begin_cell()
    .store_slice("Merhaba, dÃ¼nya!")
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_empty?()` `false` dÃ¶ner, Ã§Ã¼nkÃ¼ dilimde herhangi bir `bit` ve `ref` bulunur
slice_with_bits_and_refs.slice_empty?();
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**  
> `"slice_empty?()" belgelerde`  
> `"store_slice()" belgelerde`  
> `"store_ref()" belgelerde`  
> `"begin_cell()" belgelerde`  
> `"end_cell()" belgelerde`  
> `"begin_parse()" belgelerde`

### Sadece `bits` olmadÄ±ÄŸÄ±nÄ± (ama `refs` olabilir) nasÄ±l belirleriz

EÄŸer yalnÄ±zca `bits` kontrol etmemiz gerekiyorsa ve dilimde herhangi bir `refs` olup olmadÄ±ÄŸÄ± Ã¶nemli deÄŸilse, `slice_data_empty?()` kullanmalÄ±yÄ±z.

```func 
;; boÅŸ dilim oluÅŸturma
slice empty_slice = "";
;; `slice_data_empty?()` `true` dÃ¶ner, Ã§Ã¼nkÃ¼ dilimde herhangi bir `bit` yoktur
empty_slice.slice_data_empty?();

;; yalnÄ±zca bit iÃ§eren dilim oluÅŸturma
slice slice_with_bits_only = "Merhaba, dÃ¼nya!";
;; `slice_data_empty?()` `false` dÃ¶ner, Ã§Ã¼nkÃ¼ dilimde herhangi bir `bit` bulunur
slice_with_bits_only.slice_data_empty?();

;; yalnÄ±zca ref iÃ§eren dilim oluÅŸturma
slice slice_with_refs_only = begin_cell()
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_data_empty?()` `true` dÃ¶ner, Ã§Ã¼nkÃ¼ dilimde herhangi bir `bit` yoktur
slice_with_refs_only.slice_data_empty?();

;; hem bit hem de ref iÃ§eren dilim oluÅŸturma
slice slice_with_bits_and_refs = begin_cell()
    .store_slice("Merhaba, dÃ¼nya!")
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_data_empty?()` `false` dÃ¶ner, Ã§Ã¼nkÃ¼ dilimde herhangi bir `bit` bulunur
slice_with_bits_and_refs.slice_data_empty?();
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**  
> `"slice_data_empty?()" belgelerde`  
> `"store_slice()" belgelerde`  
> `"store_ref()" belgelerde`  
> `"begin_cell()" belgelerde`  
> `"end_cell()" belgelerde`  
> `"begin_parse()" belgelerde`

### Sadece `refs` olmadÄ±ÄŸÄ±nÄ± (ama `bits` olabilir) nasÄ±l belirleriz

EÄŸer yalnÄ±zca `refs` ile ilgileniyorsak, varlÄ±klarÄ±nÄ± `slice_refs_empty?()` kullanarak kontrol etmeliyiz.

```func 
;; boÅŸ dilim oluÅŸturma
slice empty_slice = "";
;; `slice_refs_empty?()` `true` dÃ¶ner, Ã§Ã¼nkÃ¼ dilimde herhangi bir `ref` yoktur
empty_slice.slice_refs_empty?();

;; yalnÄ±zca bit iÃ§eren dilim oluÅŸturma
slice slice_with_bits_only = "Merhaba, dÃ¼nya!";
;; `slice_refs_empty?()` `true` dÃ¶ner, Ã§Ã¼nkÃ¼ dilimde herhangi bir `ref` yoktur
slice_with_bits_only.slice_refs_empty?();

;; yalnÄ±zca ref iÃ§eren dilim oluÅŸturma
slice slice_with_refs_only = begin_cell()
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_refs_empty?()` `false` dÃ¶ner, Ã§Ã¼nkÃ¼ dilimde herhangi bir `ref` bulunur
slice_with_refs_only.slice_refs_empty?();

;; hem bit hem de ref iÃ§eren dilim oluÅŸturma
slice slice_with_bits_and_refs = begin_cell()
    .store_slice("Merhaba, dÃ¼nya!")
    .store_ref(null())
    .end_cell()
    .begin_parse();
;; `slice_refs_empty?()` `false` dÃ¶ner, Ã§Ã¼nkÃ¼ dilimde herhangi bir `ref` bulunur
slice_with_bits_and_refs.slice_refs_empty?();
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**  
> `"slice_refs_empty?()" belgelerde`  
> `"store_slice()" belgelerde`  
> `"store_ref()" belgelerde`  
> `"begin_cell()" belgelerde`  
> `"end_cell()" belgelerde`  
> `"begin_parse()" belgelerde`

### Bir hÃ¼crenin boÅŸ olup olmadÄ±ÄŸÄ±nÄ± nasÄ±l belirleriz

Bir `cell` iÃ§inde herhangi bir veri olup olmadÄ±ÄŸÄ±nÄ± kontrol etmek iÃ§in Ã¶nce onu bir `slice`'e dÃ¶nÃ¼ÅŸtÃ¼rmeliyiz. Sadece `bits` ile ilgilendiÄŸimizde `slice_data_empty?()`, yalnÄ±zca `refs` ile ilgilendiÄŸimizde `slice_refs_empty?()` kullanmalÄ±yÄ±z. Herhangi bir verinin var olup olmadÄ±ÄŸÄ±nÄ± kontrol etmek istediÄŸimizde `slice_empty?()` kullanmalÄ±yÄ±z.

```func
cell cell_with_bits_and_refs = begin_cell()
    .store_uint(1337, 16)
    .store_ref(null())
    .end_cell();

;; `cell` tÃ¼rÃ¼nÃ¼ `begin_parse()` ile dilime deÄŸiÅŸtirin
slice cs = cell_with_bits_and_refs.begin_parse();

;; dilimin boÅŸ olup olmadÄ±ÄŸÄ±nÄ± belirle
if (cs.slice_empty?()) {
    ;; hÃ¼cre boÅŸ
}
else {
    ;; hÃ¼cre boÅŸ deÄŸil
}
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**  
> `"slice_empty?()" belgelerde`  
> `"begin_cell()" belgelerde`  
> `"store_uint()" belgelerde`  
> `"end_cell()" belgelerde`  
> `"begin_parse()" belgelerde`

### Bir dict'in boÅŸ olup olmadÄ±ÄŸÄ±nÄ± nasÄ±l belirleriz

Dict iÃ§inde veri varlÄ±ÄŸÄ±nÄ± kontrol etmek iÃ§in `dict_empty?()` yÃ¶ntemini kullanÄ±rÄ±z. Bu yÃ¶ntem genellikle bir `null` hÃ¼cresinin boÅŸ bir sÃ¶zlÃ¼k olduÄŸunu dÃ¼ÅŸÃ¼nÃ¼ldÃ¼ÄŸÃ¼nde `cell_null?()` ile eÅŸdeÄŸerdir.

```func
cell d = new_dict();
d~udict_set(256, 0, "merhaba");
d~udict_set(256, 1, "dÃ¼nya");

if (d.dict_empty?()) { ;; SÃ¶zlÃ¼ÄŸÃ¼n boÅŸ olup olmadÄ±ÄŸÄ±nÄ± belirleyin
    ;; sÃ¶zlÃ¼k boÅŸ
}
else {
    ;; sÃ¶zlÃ¼k boÅŸ deÄŸil
}
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**  
> `"dict_empty?()" belgelerde`  
> `"new_dict()" belgelerde` boÅŸ bir sÃ¶zlÃ¼k oluÅŸturma  
> `"dict_set()" belgelerde` sÃ¶zlÃ¼k d'ye bazÄ± Ã¶ÄŸeler ekleyerek, bÃ¶ylece boÅŸ deÄŸildir

### Bir tuple'Ä±n boÅŸ olup olmadÄ±ÄŸÄ±nÄ± nasÄ±l belirleriz

`tuples` ile Ã§alÄ±ÅŸÄ±rken, iÃ§lerinde herhangi bir deÄŸerin olup olmadÄ±ÄŸÄ±nÄ± her zaman bilmek Ã¶nemlidir. EÄŸer boÅŸ bir `tuple`'dan deÄŸer Ã§Ä±karmaya Ã§alÄ±ÅŸÄ±rsak, "geÃ§erli boyutta bir tuple deÄŸil" hatasÄ±nÄ± alÄ±rÄ±z ve `exit code 7` dÃ¶ner.

```func
;; tlen fonksiyonunu tanÄ±mlayÄ±n Ã§Ã¼nkÃ¼ stdlib'de mevcut deÄŸildir
(int) tlen (tuple t) asm "TLEN";

() main () {
    tuple t = empty_tuple();
    t~tpush(13);
    t~tpush(37);

    if (t.tlen() == 0) {
        ;; tuple boÅŸ
    }
    else {
        ;; tuple boÅŸ deÄŸil
    }
}
```

> ğŸ’¡ **Not:** tlen assembly fonksiyonunu tanÄ±mlÄ±yoruz. Daha fazlasÄ±nÄ± okuyabilirsiniz `burada` ve `tÃ¼m assembly komutlarÄ± listesine` gÃ¶z atabilirsiniz.

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**  
> `"empty_tuple?()" belgelerde`  
> `"tpush()" belgelerde`  
> `"Exit kodlarÄ±" belgelerde`

### LISP tarzÄ± bir listenin boÅŸ olup olmadÄ±ÄŸÄ±nÄ± nasÄ±l belirleriz

```func
tuple numbers = null();
numbers = cons(100, numbers);

if (numbers.null?()) {
    ;; liste tarzÄ± liste boÅŸ
} else {
    ;; liste tarzÄ± liste boÅŸ deÄŸil
}
```

Liste tarzÄ± listemize `cons` fonksiyonu ile 100 sayÄ±sÄ±nÄ± ekliyoruz, bÃ¶ylece boÅŸ olmayacaktÄ±r.

### SÃ¶zleÅŸmenin durumunun boÅŸ olup olmadÄ±ÄŸÄ±nÄ± nasÄ±l belirleriz

Bir `counter`'Ä±mÄ±z olduÄŸunu varsayalÄ±m, bu deÄŸiÅŸken iÅŸlemlerin sayÄ±sÄ±nÄ± depolar. Bu deÄŸiÅŸken akÄ±llÄ± sÃ¶zleÅŸme durumunda ilk iÅŸlem sÄ±rasÄ±nda mevcut olmayacaÄŸÄ± iÃ§in, bÃ¶yle bir durumu iÅŸlememiz gerekmektedir. Durum boÅŸsa, bir `counter` deÄŸiÅŸkeni oluÅŸturur ve kaydederiz.

```func
;; `get_data()` sÃ¶zleÅŸme durumundan veri hÃ¼cresini dÃ¶ndÃ¼recektir
cell contract_data = get_data();
slice cs = contract_data.begin_parse();

if (cs.slice_empty?()) {
    ;; sÃ¶zleÅŸme verisi boÅŸ, bu yÃ¼zden sayacÄ± oluÅŸturup kaydediyoruz
    int counter = 1;
    ;; hÃ¼cre oluÅŸtur, sayacÄ± ekle ve sÃ¶zleÅŸme durumuna kaydet
    set_data(begin_cell().store_uint(counter, 32).end_cell());
}
else {
    ;; sÃ¶zleÅŸme verisi boÅŸ deÄŸil, bu yÃ¼zden sayacÄ±mÄ±zÄ± alÄ±p artÄ±rÄ±yoruz ve kaydediyoruz
    ;; sayacÄ±mÄ±zÄ±n bit cinsinden doÄŸru uzunluÄŸunu belirtmeliyiz
    int counter = cs~load_uint(32) + 1;
    set_data(begin_cell().store_uint(counter, 32).end_cell());
}
```

> ğŸ’¡ **Not:** Bir sÃ¶zleÅŸmenin durumunun boÅŸ olduÄŸunu, `cell boÅŸsa` belirleyerek anlayabiliriz.

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**  
> `"get_data()" belgelerde`  
> `"begin_parse()" belgelerde`  
> `"slice_empty?()" belgelerde`  
> `"set_data?()" belgelerde`

### Ä°Ã§ mesaj hÃ¼cresinin nasÄ±l oluÅŸturulacaÄŸÄ±

EÄŸer sÃ¶zleÅŸmenin bir iÃ§ mesaj gÃ¶ndermesini istiyorsak, Ã¶ncelikle teknik bayraklarÄ±, alÄ±cÄ± adresini ve diÄŸer verileri belirterek doÄŸru bir ÅŸekilde bir hÃ¼cre oluÅŸturmalÄ±yÄ±z.

```func
;; GeÃ§erli adresi bir dize iÃ§inden almak iÃ§in `a` literal'Ä±nÄ± kullanÄ±yoruz 
slice addr = "EQArzP5prfRJtDM5WrMNWyr9yUTAi0c9o6PfR4hkWy9UQXHx"a;
int amount = 1000000000;
;; iÅŸlemleri tanÄ±mlamak iÃ§in `op` kullanÄ±yoruz
int op = 0;

cell msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(addr)
    .store_coins(amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; varsayÄ±lan mesaj baÅŸlÄ±klarÄ± (mesaj gÃ¶nderme sayfasÄ±na bakÄ±nÄ±z)
    .store_uint(op, 32)
.end_cell();

send_raw_message(msg, 3); ;; mod 3 - Ã¼cretleri ayrÄ± Ã¶de ve hatalarÄ± gÃ¶z ardÄ± et 
```

> ğŸ’¡ **Not:** Bu Ã¶rnekte geÃ§erli adresi almak iÃ§in `a` literal'Ä±nÄ± kullanÄ±yoruz. Dize literal'larÄ± hakkÄ±nda daha fazla bilgiye `belgelerde` ulaÅŸabilirsiniz.

> ğŸ’¡ **Not:** Daha fazlasÄ±nÄ± `belgelerde` bulabilirsiniz. AyrÄ±ca bu baÄŸlantÄ±yla `dÃ¼zen` sayfasÄ±na gidebilirsiniz.

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**  
> `"begin_cell()" belgelerde`  
> `"store_uint()" belgelerde`  
> `"store_slice()" belgelerde`  
> `"store_coins()" belgelerde`  
> `"end_cell()" belgelerde`  
> `"send_raw_message()" belgelerde`

### Bir mesaj hÃ¼cresinin gÃ¶vdesini referans olarak nasÄ±l iÃ§erebiliriz

MesajÄ±n gÃ¶vdesinde bayraklar ve diÄŸer teknik verilerden sonra `int`, `slice` ve `cell` gÃ¶nderebiliriz. Ä°kincisi durumunda, `store_ref()` Ã¶ncesinde bit `1`'e ayarlanmalÄ±dÄ±r, bu da `cell`'in devam edeceÄŸini belirtir.

Mesaj gÃ¶vdesini, yeterli alan olduÄŸundan eminsek, aynÄ± `cell` iÃ§inde baÅŸlÄ±kla birlikte gÃ¶nderebiliriz. Bu durumda, bit `0`'a ayarlanmalÄ±dÄ±r.

```func
;; GeÃ§erli adresi bir dize iÃ§inden almak iÃ§in `a` literal'Ä±nÄ± kullanÄ±yoruz 
slice addr = "EQArzP5prfRJtDM5WrMNWyr9yUTAi0c9o6PfR4hkWy9UQXHx"a;
int amount = 1000000000;
int op = 0;
cell message_body = begin_cell() ;; mesaj ile birlikte bir hÃ¼cre oluÅŸturma
    .store_uint(op, 32)
    .store_slice("â¤")
.end_cell();
    
cell msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(addr)
    .store_coins(amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1) ;; varsayÄ±lan mesaj baÅŸlÄ±klarÄ± (mesaj gÃ¶nderme sayfasÄ±na bakÄ±nÄ±z)
    .store_uint(1, 1) ;; hÃ¼cre gideceÄŸini belirtmek iÃ§in biti 1'e ayarla
    .store_ref(message_body)
.end_cell();

send_raw_message(msg, 3); ;; mod 3 - Ã¼cretleri ayrÄ± Ã¶de ve hatalarÄ± gÃ¶z ardÄ± et 
```

> ğŸ’¡ **Not:** Bu Ã¶rnekte geÃ§erli adresi almak iÃ§in `a` literal'Ä±nÄ± kullanÄ±yoruz. Dize literal'larÄ± hakkÄ±nda daha fazla bilgiye `belgelerde` ulaÅŸabilirsiniz.

> ğŸ’¡ **Not:** Bu Ã¶rnekte, gelen tonlarÄ± alÄ±p, belirtilen kadar (miktar) gÃ¶ndermek iÃ§in mod 3 kullanÄ±ldÄ±. Komisyonu sÃ¶zleÅŸme bakiyesinden Ã¶deyerek hatalarÄ± gÃ¶z ardÄ± ediyor. Mod 64, alÄ±nan tÃ¼m tonlarÄ±n, komisyon Ã§Ä±karÄ±larak geri dÃ¶ndÃ¼rÃ¼lmesi iÃ§in gereklidir ve mod 128, tÃ¼m bakiyeyi gÃ¶nderecektir.

> ğŸ’¡ **Not:** `Bir mesaj oluÅŸturuyoruz`, ancak mesaj gÃ¶vdesini ayrÄ± olarak ekliyoruz.

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**  
> `"begin_cell()" belgelerde`  
> `"store_uint()" belgelerde`  
> `"store_slice()" belgelerde`  
> `"store_coins()" belgelerde`  
> `"end_cell()" belgelerde`  
> `"send_raw_message()" belgelerde`

### Bir mesaj hÃ¼cresinin gÃ¶vdesini slice olarak nasÄ±l iÃ§erebiliriz

Mesaj gÃ¶nderirken, mesajÄ±n gÃ¶vdesi ya `cell` ya da `slice` olarak gÃ¶nderilebilir. Bu Ã¶rnekte, mesajÄ±n gÃ¶vdesini `slice` iÃ§inde gÃ¶nderiyoruz.

```func 
;; GeÃ§erli adresi bir dize iÃ§inden almak iÃ§in `a` literal'Ä±nÄ± kullanÄ±yoruz 
slice addr = "EQArzP5prfRJtDM5WrMNWyr9yUTAi0c9o6PfR4hkWy9UQXHx"a;
int amount = 1000000000;
int op = 0;
slice message_body = "â¤"; 

cell msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(addr)
    .store_coins(amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; varsayÄ±lan mesaj baÅŸlÄ±klarÄ± (mesaj gÃ¶nderme sayfasÄ±na bakÄ±nÄ±z)
    .store_uint(op, 32)
    .store_slice(message_body)
.end_cell();

send_raw_message(msg, 3); ;; mod 3 - Ã¼cretleri ayrÄ± Ã¶de ve hatalarÄ± gÃ¶z ardÄ± et 
```

> ğŸ’¡ **Not:** Bu Ã¶rnekte geÃ§erli adresi almak iÃ§in `a` literal'Ä±nÄ± kullanÄ±yoruz. Dize literal'larÄ± hakkÄ±nda daha fazla bilgiye `belgelerde` ulaÅŸabilirsiniz.

> ğŸ’¡ **Not:** Bu Ã¶rnekte, gelen tonlarÄ± alÄ±p, belirtilen kadar (miktar) gÃ¶ndermek iÃ§in mod 3 kullanÄ±ldÄ±. Komisyonu sÃ¶zleÅŸme bakiyesinden Ã¶deyerek hatalarÄ± gÃ¶z ardÄ± ediyor. Mod 64, alÄ±nan tÃ¼m tonlarÄ±n, komisyon Ã§Ä±karÄ±larak geri dÃ¶ndÃ¼rÃ¼lmesi iÃ§in gereklidir ve mod 128, tÃ¼m bakiyeyi gÃ¶nderecektir.

> ğŸ’¡ **Not:** `Bir mesaj oluÅŸturuyoruz`, ancak mesajÄ± bir dilim olarak gÃ¶nderiyoruz.

### Ä°kili demetler Ã¼zerinde nasÄ±l yineleme yapÄ±lÄ±r (her iki yÃ¶nde)

Bir dizi veya yÄ±ÄŸÄ±n ile Ã§alÄ±ÅŸmak istersek, FunC'te demet gerekli olacaktÄ±r. Ä°lk olarak, bunlarla Ã§alÄ±ÅŸabilmek iÃ§in deÄŸerleri yineleyebilmemiz gerekiyor.

```func
(int) tlen (tuple t) asm "TLEN";
forall X -> (tuple) to_tuple (X x) asm "NOP";

() main () {
    tuple t = to_tuple([1, 2, 3, 4, 5, 6, 7, 8, 9, 10]);
    int len = t.tlen();
    
    int i = 0;
    while (i < len) {
        int x = t.at(i);
        ;; x ile bir ÅŸeyler yap
        i = i + 1;
    }

    i = len - 1;
    while (i >= 0) {
        int x = t.at(i);
        ;; x ile bir ÅŸeyler yap
        i = i - 1;
    }
}
```

> ğŸ’¡ **Not**
> 
> `tlen` assembly fonksiyonunu tanÄ±mlÄ±yoruz. Daha fazla bilgi almak iÃ§in [buradan](https://v3/documentation/smart-contracts/func/docs/functions#assembler-function-body-definition) okuyabilir ve [tÃ¼m montaj komutlarÄ±nÄ±n listesini](https://v3/documentation/tvm/instructions) gÃ¶rebilirsiniz.

> AyrÄ±ca `to_tuple` fonksiyonunu da tanÄ±mlÄ±yoruz. Bu sadece herhangi bir giriÅŸin veri tipini demete dÃ¶nÃ¼ÅŸtÃ¼rÃ¼r, bu nedenle kullanÄ±rken dikkatli olun.

---

### `asm` anahtar kelimesi kullanarak kendi fonksiyonlarÄ±nÄ±zÄ± nasÄ±l yazarsÄ±nÄ±z

Herhangi bir Ã¶zellik kullanÄ±rken aslÄ±nda bizim iÃ§in Ã¶nceden hazÄ±rlanmÄ±ÅŸ yÃ¶ntemleri `stdlib.fc` iÃ§inde kullanÄ±yoruz. Ancak gerÃ§ekte, elimizde daha fazla fÄ±rsat bulunmaktadÄ±r ve bunlarÄ± kendimiz yazmayÄ± Ã¶ÄŸrenmemiz gerekiyor.

Ã–rneÄŸin, `tuple`'a bir eleman ekleyen `tpush` adlÄ± bir metodumuz var, ancak `tpop` yok. Bu durumda, ÅŸunu yapmalÄ±yÄ±z:

```func
;; ~ demek deÄŸiÅŸtirme metodudur
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP"; 
```

DÃ¶ngÃ¼ iÃ§in `tuple` uzunluÄŸunu Ã¶ÄŸrenmek istiyorsak, `TLEN` asm talimatÄ±yla yeni bir fonksiyon yazmalÄ±yÄ±z:

```func
int tuple_length (tuple t) asm "TLEN";
```

`stdlib.fc`'den zaten bildiÄŸimiz bazÄ± fonksiyon Ã¶rnekleri:

```func
slice begin_parse(cell c) asm "CTOS";
builder begin_cell() asm "NEWC";
cell end_cell(builder b) asm "ENDC";
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar:**
>
> `"deÄŸiÅŸtirme metodlarÄ±" belgelerinde`  
> `"stdlib" belgelerinde`  
> `"TVM talimatlarÄ±" belgelerinde`  

---

### N-nestel demetler Ã¼zerinde yineleme

Bazen iÃ§ iÃ§e demetleri yinelemek isteriz. AÅŸaÄŸÄ±daki Ã¶rnek, bellekleri baÅŸtan baÅŸlayarak `[[2,6],[1,[3,[3,5]]], 3]` biÃ§imindeki bir demet iÃ§indeki tÃ¼m Ã¶ÄŸeleri yineleyecektir.

```func
int tuple_length (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";
forall X -> int is_tuple (X x) asm "ISTUPLE";
forall X -> tuple cast_to_tuple (X x) asm "NOP";
forall X -> int cast_to_int (X x) asm "NOP";
forall X -> (tuple) to_tuple (X x) asm "NOP";

;; global deÄŸiÅŸken tanÄ±mla
global int max_value;

() iterate_tuple (tuple t) impure {
    repeat (t.tuple_length()) {
        var value = t~tpop();
        if (is_tuple(value)) {
            tuple tuple_value = cast_to_tuple(value);
            iterate_tuple(tuple_value);
        }
        else {
            if(value > max_value) {
                max_value = value;
            }
        }
    }
}

() main () {
    tuple t = to_tuple([[2,6], [1, [3, [3, 5]]], 3]);
    int len = t.tuple_length();
    max_value = 0; ;; max_value'yi sÄ±fÄ±rla;
    iterate_tuple(t); ;; demeti yinele ve maksimum deÄŸeri bul
    ~dump(max_value); ;; 6
}
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**
>
> `"Global deÄŸiÅŸkenler" belgelerinde`  
> `"~dump" belgelerinde`  
> `"TVM talimatlarÄ±" belgelerinde`  

---

### Demetlerle temel iÅŸlemler

```func
(int) tlen (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";

() main () {
    ;; boÅŸ bir demet oluÅŸturma
    tuple names = empty_tuple(); 
    
    ;; yeni Ã¶ÄŸeler ekle
    names~tpush("Naito Narihira");
    names~tpush("Shiraki Shinichi");
    names~tpush("Akamatsu Hachemon");
    names~tpush("Takaki Yuichi");
    
    ;; son Ã¶ÄŸeyi Ã§Ä±kar
    slice last_name = names~tpop();

    ;; ilk Ã¶ÄŸeyi al
    slice first_name = names.first();

    ;; indeks ile bir Ã¶ÄŸe al
    slice best_name = names.at(2);

    ;; liste uzunluÄŸunu al
    int number_names = names.tlen();
}
```

---

### X tipini Ã§Ã¶zme

AÅŸaÄŸÄ±daki Ã¶rnek, bir deÄŸerin bir demette bulunup bulunmadÄ±ÄŸÄ±nÄ± kontrol eder, ancak demet deÄŸerleri X (cell, slice, int, tuple, int) iÃ§erir. DeÄŸeri kontrol etmemiz ve uygun ÅŸekilde dÃ¶nÃ¼ÅŸtÃ¼rmemiz gerekiyor.

```func
forall X -> int is_null (X x) asm "ISNULL";
forall X -> int is_int (X x) asm "<{ TRY:<{ 0 PUSHINT ADD DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_cell (X x) asm "<{ TRY:<{ CTOS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_slice (X x) asm "<{ TRY:<{ SBITS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_tuple (X x) asm "ISTUPLE";
forall X -> int cast_to_int (X x) asm "NOP";
forall X -> cell cast_to_cell (X x) asm "NOP";
forall X -> slice cast_to_slice (X x) asm "NOP";
forall X -> tuple cast_to_tuple (X x) asm "NOP";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";

forall X -> () resolve_type (X value) impure {
    ;; deÄŸer burada X tipindedir, Ã§Ã¼nkÃ¼ kesin deÄŸer nedir bilmiyoruz - deÄŸeri kontrol etmemiz ve sonra dÃ¶nÃ¼ÅŸtÃ¼rmemiz gerekir
    
    if (is_null(value)) {
        ;; null ile bir ÅŸeyler yap
    }
    elseif (is_int(value)) {
        int valueAsInt = cast_to_int(value);
        ;; int ile bir ÅŸeyler yap
    }
    elseif (is_slice(value)) {
        slice valueAsSlice = cast_to_slice(value);
        ;; slice ile bir ÅŸeyler yap
    }
    elseif (is_cell(value)) {
        cell valueAsCell = cast_to_cell(value);
        ;; cell ile bir ÅŸeyler yap
    }
    elseif (is_tuple(value)) {
        tuple valueAsTuple = cast_to_tuple(value);
        ;; tuple ile bir ÅŸeyler yap
    }
}

() main () {
    ;; boÅŸ bir demet oluÅŸtur
    tuple stack = empty_tuple();
    ;; diyelim ki bir demetimiz var ve bunlarÄ±n kesin tiplerini bilmiyoruz
    stack~tpush("BazÄ± metinler");
    stack~tpush(4);
    ;; deÄŸerimizin tipi ne olduÄŸunu bilmediÄŸimiz iÃ§in var kullanÄ±yoruz
    var value = stack~tpop();
    resolve_type(value);
}
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**
>
> `"TVM talimatlarÄ±" belgelerinde`  

---

### GeÃ§erli zamanÄ± nasÄ±l alÄ±rsÄ±nÄ±z

```func
int current_time = now();
  
if (current_time > 1672080143) {
    ;; bazÄ± iÅŸlemleri yap 
}
```

---

### Rastgele sayÄ± nasÄ±l Ã¼retilir

:::caution taslak
LÃ¼tfen unutmayÄ±n ki bu rastgele sayÄ± Ã¼retme yÃ¶ntemi gÃ¼venli deÄŸildir.

Daha fazla bilgi iÃ§in `Rastgele SayÄ± Ãœretimi` sayfasÄ±na gÃ¶z atÄ±n.
:::

```func
randomize_lt(); ;; bunu bir kez yap

int a = rand(10);
int b = rand(1000000);
int c = random();
```

---

### ModÃ¼l iÅŸlemleri

Ã–rneÄŸin, tÃ¼m 256 sayÄ±nÄ±n aÅŸaÄŸÄ±daki hesaplamasÄ±nÄ± Ã§alÄ±ÅŸtÄ±rmak istediÄŸimizi varsayalÄ±m: `(xp + zp)*(xp-zp)`. Bu iÅŸlemlerin Ã§oÄŸunun kriptografi iÃ§in kullanÄ±ldÄ±ÄŸÄ±nÄ± gÃ¶z Ã¶nÃ¼nde bulundurursak, aÅŸaÄŸÄ±daki Ã¶rnekte montgomery eÄŸrileri iÃ§in modÃ¼l operatÃ¶rÃ¼nÃ¼ kullanÄ±yoruz.  
xp+zp geÃ§erli bir deÄŸiÅŸken adÄ± olduÄŸunu unutmayÄ±n (boÅŸluk yok).

```func
(int) modulo_operations (int xp, int zp) {  
   ;; 2^255 - 19 montgomery eÄŸrileri iÃ§in bir asal sayÄ±dÄ±r, yani tÃ¼m iÅŸlemler bunun asal sayÄ±sÄ± etrafÄ±nda yapÄ±lmalÄ±dÄ±r
   int prime = 57896044618658097711785492504343953926634992332820282019728792003956564819949; 

   ;; muldivmod bir sonraki iki satÄ±rÄ± kendisi iÅŸleme alÄ±r
   ;; int xp+zp = (xp + zp) % prime;
   ;; int xp-zp = (xp - zp + prime) % prime;
   (_, int xp+zp*xp-zp) = muldivmod(xp + zp, xp - zp, prime);
   return xp+zp*xp-zp;
}
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**
>
> `"muldivmod" belgelerinde`  

---

### HatalarÄ± nasÄ±l atarsÄ±nÄ±z

```func
int number = 198;

throw_if(35, number > 50); ;; hata yalnÄ±zca sayÄ± 50'den bÃ¼yÃ¼kse tetiklenecektir

throw_unless(39, number == 198); ;; hata yalnÄ±zca sayÄ± 198'e EÅÄ°T DEÄÄ°LSE tetiklenecektir

throw(36); ;; hata her koÅŸulda tetiklenecektir
```

`Standart tvm istisna kodlarÄ±`  

---

### Demetleri tersine Ã§evirmek

Demet verileri bir yÄ±ÄŸÄ±n olarak depoladÄ±ÄŸÄ±ndan, bazen diÄŸer uÃ§tan verileri okumak iÃ§in demeti tersine Ã§evirmemiz gerekir.

```func
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";
int tuple_length (tuple t) asm "TLEN";
forall X -> (tuple) to_tuple (X x) asm "NOP";

(tuple) reverse_tuple (tuple t1) {
    tuple t2 = empty_tuple();
    repeat (t1.tuple_length()) {
        var value = t1~tpop();
        t2~tpush(value);
    }
    return t2;
}

() main () {
    tuple t = to_tuple([1, 2, 3, 4, 5, 6, 7, 8, 9, 10]);
    tuple reversed_t = reverse_tuple(t);
    ~dump(reversed_t); ;; [10 9 8 7 6 5 4 3 2 1]
}
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**
>
> `"tpush()" belgelerinde`  

---

### Belirli bir indeksle bir Ã¶ÄŸeyi listeden nasÄ±l kaldÄ±rÄ±rsÄ±nÄ±z

```func
int tlen (tuple t) asm "TLEN";

(tuple, ()) remove_item (tuple old_tuple, int place) {
    tuple new_tuple = empty_tuple();

    int i = 0;
    while (i < old_tuple.tlen()) {
        int el = old_tuple.at(i);
        if (i != place) {
            new_tuple~tpush(el);
        }
        i += 1;  
    }
    return (new_tuple, ());
}

() main () {
    tuple numbers = empty_tuple();

    numbers~tpush(19);
    numbers~tpush(999);
    numbers~tpush(54);

    ~dump(numbers); ;; [19 999 54]

    numbers~remove_item(1); 

    ~dump(numbers); ;; [19 54]
}
```

---

### Dizi eÅŸitliÄŸini belirleme

Ä°ki farklÄ± ÅŸekilde eÅŸitliÄŸi belirleyebiliriz. Biri dilim hash'ine dayanmaktadÄ±r, diÄŸeri ise SDEQ asm talimatÄ±nÄ± kullanarak.

```func
int are_slices_equal_1? (slice a, slice b) {
    return a.slice_hash() == b.slice_hash();
}

int are_slices_equal_2? (slice a, slice b) asm "SDEQ";

() main () {
    slice a = "BazÄ± metinler";
    slice b = "BazÄ± metinler";
    ~dump(are_slices_equal_1?(a, b)); ;; -1 = doÄŸru

    a = "Metin";
    ;; DoÄŸru adrese dilim iÃ§inden geÃ§erli adresi almak iÃ§in literal 'a' kullanÄ±yoruz
    b = "EQDKbjIcfM6ezt8KjKJJLshZJJSqX7XOA4ff-W72r5gqPrHF"a;
    ~dump(are_slices_equal_2?(a, b)); ;; 0 = yanlÄ±ÅŸ
}
```

#### ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**

 * `"slice_hash()" belgelerinde`  
 * `"SDEQ" belgelerinde`  

---

### HÃ¼crelerin eÅŸit olup olmadÄ±ÄŸÄ±nÄ± belirleme 

HÃ¼cre eÅŸitliÄŸini, hash'lerine dayanarak kolayca belirleyebiliriz.

```func
int are_cells_equal? (cell a, cell b) {
    return a.cell_hash() == b.cell_hash();
}

() main () {
    cell a = begin_cell()
            .store_uint(123, 16)
            .end_cell();

    cell b = begin_cell()
            .store_uint(123, 16)
            .end_cell();

    ~dump(are_cells_equal?(a, b)); ;; -1 = doÄŸru
}
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**
>
> `"cell_hash()" belgelerinde`  

---

### Demetlerin eÅŸit olup olmadÄ±ÄŸÄ±nÄ± belirleme

Daha geliÅŸmiÅŸ bir Ã¶rnek, her bir demet deÄŸerini yineleyip karÅŸÄ±laÅŸtÄ±rmaktÄ±r. X olduklarÄ±ndan, kontrol etmemiz ve karÅŸÄ±lÄ±k gelen tipe dÃ¶nÃ¼ÅŸtÃ¼rmemiz gerekiyor ve eÄŸer demet ise yinelemeli olarak dÃ¶nÃ¼ÅŸtÃ¼rmemiz gerekiyor.

```func
int tuple_length (tuple t) asm "TLEN";
forall X -> (tuple, X) ~tpop (tuple t) asm "TPOP";
forall X -> int cast_to_int (X x) asm "NOP";
forall X -> cell cast_to_cell (X x) asm "NOP";
forall X -> slice cast_to_slice (X x) asm "NOP";
forall X -> tuple cast_to_tuple (X x) asm "NOP";
forall X -> int is_null (X x) asm "ISNULL";
forall X -> int is_int (X x) asm "<{ TRY:<{ 0 PUSHINT ADD DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_cell (X x) asm "<{ TRY:<{ CTOS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_slice (X x) asm "<{ TRY:<{ SBITS DROP -1 PUSHINT }>CATCH<{ 2DROP 0 PUSHINT }> }>CONT 1 1 CALLXARGS";
forall X -> int is_tuple (X x) asm "ISTUPLE";
int are_slices_equal? (slice a, slice b) asm "SDEQ";

int are_cells_equal? (cell a, cell b) {
    return a.cell_hash() == b.cell_hash();
}

(int) are_tuples_equal? (tuple t1, tuple t2) {
    int equal? = -1; ;; baÅŸlangÄ±Ã§ deÄŸeri doÄŸru
    
    if (t1.tuple_length() != t2.tuple_length()) {
        ;; demetler uzunluk aÃ§Ä±sÄ±ndan farklÄ±ysa eÅŸit olamazlar
        return 0;
    }

    int i = t1.tuple_length();
    
    while (i > 0 & equal?) {
        var v1 = t1~tpop();
        var v2 = t2~tpop();
        
        if (is_null(t1) & is_null(t2)) {
            ;; null her zaman eÅŸittir
        }
        elseif (is_int(v1) & is_int(v2)) {
            if (cast_to_int(v1) != cast_to_int(v2)) {
                equal? = 0;
            }
        }
        elseif (is_slice(v1) & is_slice(v2)) {
            if (~ are_slices_equal?(cast_to_slice(v1), cast_to_slice(v2))) {
                equal? = 0;
            }
        }
        elseif (is_cell(v1) & is_cell(v2)) {
            if (~ are_cells_equal?(cast_to_cell(v1), cast_to_cell(v2))) {
                equal? = 0;
            }
        }
        elseif (is_tuple(v1) & is_tuple(v2)) {
            ;; iÃ§ iÃ§e demetlerin eÅŸitliÄŸini yinelemeli olarak belirle
            if (~ are_tuples_equal?(cast_to_tuple(v1), cast_to_tuple(v2))) {
                equal? = 0;
            }
        }
        else {
            equal? = 0;
        }

        i -= 1;
    }

    return equal?;
}

() main () {
    tuple t1 = cast_to_tuple([[2, 6], [1, [3, [3, 5]]], 3]);
    tuple t2 = cast_to_tuple([[2, 6], [1, [3, [3, 5]]], 3]);

    ~dump(are_tuples_equal?(t1, t2)); ;; -1 
}
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**
>
> `"cell_hash()" belgelerinde`  
> `"TVM talimatlarÄ±" belgelerinde`  

---

### Ä°Ã§ adres nasÄ±l Ã¼retilir

SÃ¶zleÅŸmemiz yeni bir sÃ¶zleÅŸme daÄŸÄ±tmalÄ±ysa ama adresini bilmiyorsak iÃ§ bir adres Ã¼retmemiz gerekiyor. Diyelim ki zaten `state_init` var - yeni sÃ¶zleÅŸmenin kodu ve verileri. 

Mesaj AddressInt TLB iÃ§in karÅŸÄ±lÄ±k gelen bir iÃ§ adres oluÅŸturur.

```func
(slice) generate_internal_address (int workchain_id, cell state_init) {
    ;; addr_std$10 anycast:(Maybe Anycast) workchain_id:int8 address:bits256  = MsgAddressInt;

    return begin_cell()
        .store_uint(2, 2) ;; addr_std$10
        .store_uint(0, 1) ;; herhangi bir mesaj yok
        .store_int(workchain_id, 8) ;; iÅŸÃ§ilerin kimliÄŸi: -1
        .store_uint(cell_hash(state_init), 256)
    .end_cell().begin_parse();
}

() main () {
    slice deploy_address = generate_internal_address(workchain(), state_init);
    ;; o zaman yeni sÃ¶zleÅŸmeyi daÄŸÄ±tabiliriz
}
```

> ğŸ’¡ **Not**
> 
> Bu Ã¶rnekte, iÅŸÃ§i kimliÄŸini almak iÃ§in `workchain()` kullanÄ±yoruz. Ä°ÅŸÃ§i kimliÄŸi ile ilgili daha fazla bilgi iÃ§in [belgelere](https://v3/documentation/smart-contracts/addresses#workchain-id) gÃ¶z atabilirsiniz.

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**
>
> `"cell_hash()" belgelerinde`  

---

### DÄ±ÅŸ adres nasÄ±l Ã¼retilir

Bu formatta bir adresi nasÄ±l oluÅŸturacaÄŸÄ±mÄ±zÄ± anlamak iÃ§in [block.tlb](https://github.com/ton-blockchain/ton/blob/24dc184a2ea67f9c47042b4104bbb4d82289fac1/crypto/block/block.tlb#L101C1-L101C12) dosyasÄ±ndan TL-B ÅŸemasÄ±nÄ± kullanÄ±yoruz. 

```func
(int) ubitsize (int a) asm "UBITSIZE";

slice generate_external_address (int address) {
    ;; addr_extern$01 len:(## 9) external_address:(bits len) = MsgAddressExt;
    
    int address_length = ubitsize(address);
    
    return begin_cell()
        .store_uint(1, 2) ;; addr_extern$01
        .store_uint(address_length, 9)
        .store_uint(address, address_length)
    .end_cell().begin_parse();
}
```

Adresin kapladÄ±ÄŸÄ± bit sayÄ±sÄ±nÄ± belirlememiz gerektiÄŸinden, aÅŸaÄŸÄ±daki gibi `asm fonksiyonu` ile `UBITSIZE` opcodeunu uma tanÄ±mlamak da gereklidir, bu da sayÄ±yÄ± saklamak iÃ§in gereken minimum bit sayÄ±sÄ±nÄ± dÃ¶ndÃ¼recektir.

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**
>
> `"TVM talimatlarÄ±" belgelerinde`  

---

### Yerel depolamada sÃ¶zlÃ¼ÄŸÃ¼ nasÄ±l saklar ve yÃ¼kleriz

SÃ¶zlÃ¼ÄŸÃ¼ yÃ¼kleme mantÄ±ÄŸÄ±

```func
slice local_storage = get_data().begin_parse();
cell dictionary_cell = new_dict();
if (~ slice_empty?(local_storage)) {
    dictionary_cell = local_storage~load_dict();
}
```

SÃ¶zlÃ¼ÄŸÃ¼ saklama mantÄ±ÄŸÄ± ise aÅŸaÄŸÄ±daki Ã¶rneÄŸe benzemektedir:

```func
set_data(begin_cell().store_dict(dictionary_cell).end_cell());
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**
>
> `"get_data()" belgelerinde`  
> `"new_dict()" belgelerinde`  
> `"slice_empty?()" belgelerinde`  
> `"load_dict()" belgelerinde`  
> `"~" belgelerinde`  

---

### Basit bir mesaj nasÄ±l gÃ¶nderilir

Bir mesaj gÃ¶ndermenin alÄ±ÅŸÄ±ldÄ±k yolu aslÄ±nda basit bir mesajdÄ±r. MesajÄ±n gÃ¶vdesinin `yorum` olduÄŸunu belirtmek iÃ§in, mesaj metninden Ã¶nce `32 bit` kÄ±smÄ±nÄ± `0` olarak ayarlamalÄ±yÄ±z.

```func
cell msg = begin_cell()
    .store_uint(0x18, 6) ;; bayraklar
    .store_slice("EQBIhPuWmjT7fP-VomuTWseE8JNWv2q7QYfsVQ1IZwnMk8wL"a) ;; hedef adres
    .store_coins(100) ;; gÃ¶nderilecek nanoTon miktarÄ±
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; varsayÄ±lan mesaj baÅŸlÄ±klarÄ± (mesaj gÃ¶nderme sayfasÄ±na bak)
    .store_uint(0, 32) ;; sÄ±fÄ±r opcode - yorumla basit transfer mesajÄ± anlamÄ±na gelir
    .store_slice("FunC'tan Merhaba!") ;; yorum
.end_cell();
send_raw_message(msg, 3); ;; mod 3 - Ã¼cretleri ayrÄ± Ã¶de, hatalarÄ± gÃ¶rmezden gel
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**
>
> `"Mesaj yapÄ±sÄ±" belgelerinde`  

---

### Gelen hesapla mesaj nasÄ±l gÃ¶nderilir

AÅŸaÄŸÄ±daki sÃ¶zleÅŸme Ã¶rneÄŸi, kullanÄ±cÄ± ve ana sÃ¶zleÅŸme arasÄ±nda herhangi bir iÅŸlem yapmamÄ±z gerektiÄŸinde, yani bir proxy sÃ¶zleÅŸmesine ihtiyaÃ§ duyduÄŸumuzda bize yararlÄ±dÄ±r.

```func
() recv_internal (slice in_msg_body) {
    {-
        Bu, bir proxy sÃ¶zleÅŸmesinin basit bir Ã¶rneÄŸidir.
        in_msg_body'nÄ±n mesaj modunu, gÃ¶vdesini ve gÃ¶nderilecek hedef adresini iÃ§ermesini bekleyecektir.
    -}

    int mode = in_msg_body~load_uint(8); ;; ilk bayt msg modunu iÃ§erecektir
    slice addr = in_msg_body~load_msg_addr(); ;; ardÄ±ndan hedef adresi ayrÄ±ÅŸtÄ±rÄ±rÄ±z
    slice body = in_msg_body; ;; in_msg_body'da kalan her ÅŸey yeni mesajÄ±n gÃ¶vdesi olacaktÄ±r

    cell msg = begin_cell()
        .store_uint(0x18, 6)
        .store_slice(addr)
        .store_coins(100) ;; yalnÄ±zca Ã¶rnek olmasÄ± aÃ§Ä±sÄ±ndan
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; varsayÄ±lan mesaj baÅŸlÄ±klarÄ± (gÃ¶nderim sayfasÄ±na bak)
        .store_slice(body)
    .end_cell();
    send_raw_message(msg, mode);
}
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**
> 
> `"Mesaj yapÄ±sÄ±" belgelerinde`  
> `"load_msg_addr()" belgelerinde`  

---

### Tam bakiye ile mesaj nasÄ±l gÃ¶nderilir

AkÄ±llÄ± sÃ¶zleÅŸmenin tÃ¼m bakiyesini gÃ¶ndermemiz gerekiyorsa, bu durumda `mod 128` kullanmamÄ±z gerekecek. Bu tÃ¼r bir durumda, Ã¶demeleri kabul eden ve ana sÃ¶zleÅŸmeye ileten bir proxy sÃ¶zleÅŸmesi olacaktÄ±r.

```func
cell msg = begin_cell()
    .store_uint(0x18, 6) ;; bayraklar
    .store_slice("EQBIhPuWmjT7fP-VomuTWseE8JNWv2q7QYfsVQ1IZwnMk8wL"a) ;; hedef adres
    .store_coins(0) ;; ÅŸu anda bu deÄŸeri umursamÄ±yoruz
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) ;; varsayÄ±lan mesaj baÅŸlÄ±klarÄ± (mesaj gÃ¶nderme sayfasÄ±na bak)
    .store_uint(0, 32) ;; sÄ±fÄ±r opcode - yorumla basit transfer mesajÄ± anlamÄ±na gelir
    .store_slice("FunC'tan Merhaba!") ;; yorum
.end_cell();
send_raw_message(msg, 128); ;; mod = 128, mevcut akÄ±llÄ± sÃ¶zleÅŸmenin kalan tÃ¼m bakiyesini taÅŸÄ±yacak mesajlar iÃ§in kullanÄ±lÄ±r
```

> ğŸ’¡ **YararlÄ± baÄŸlantÄ±lar**
>
> `"Mesaj yapÄ±sÄ±" belgelerinde`  
> `"Mesaj modlarÄ±" belgelerinde`  

### Uzun metin yorumuyla nasÄ±l mesaj gÃ¶nderilir

BildiÄŸimiz gibi, bir `cell` iÃ§erisine yalnÄ±zca 127 karakter sÄ±ÄŸabilir ( ğŸ“Œ **FaydalÄ± baÄŸlantÄ±lar**
>
> `"Dahili mesajlar" belgelerinde`

---

### Sadece bir dilimden veri bitleri nasÄ±l alÄ±nÄ±r (refs olmadan)

EÄŸer `slice` iÃ§erisindeki `refs` ile ilgilenmiyorsak, ayrÄ± bir veri elde edip onunla Ã§alÄ±ÅŸabiliriz.

```func
slice s = begin_cell()
    .store_slice("BazÄ± veri bitleri...")
    .store_ref(begin_cell().end_cell()) ;; bazÄ± referanslar
    .store_ref(begin_cell().end_cell()) ;; bazÄ± referanslar
.end_cell().begin_parse();

slice s_only_data = s.preload_bits(s.slice_bits());
```

> ğŸ“Œ **FaydalÄ± baÄŸlantÄ±lar**
>
> `"Slice primitives" belgelerinde`
>
> `"preload_bits()" belgelerinde`
>
> `"slice_bits()" belgelerinde`

---

### Kendi deÄŸiÅŸtirme metodunuzu nasÄ±l tanÄ±mlarsÄ±nÄ±z

DeÄŸiÅŸtirme yÃ¶ntemleri, verilerin aynÄ± deÄŸiÅŸken iÃ§inde deÄŸiÅŸtirilmesine olanak tanÄ±r. Bu, diÄŸer programlama dillerindeki referanslama ile karÅŸÄ±laÅŸtÄ±rÄ±labilir.

```func
(slice, (int)) load_digit (slice s) {
    int x = s~load_uint(8); ;; dilimden 8 bit (bir karakter) yÃ¼kle
    x -= 48; ;; karakter '0' kodu 48 olduÄŸundan, sayÄ±yÄ± elde etmek iÃ§in bunu Ã§Ä±karÄ±yoruz
    return (s, (x)); ;; modifiye edilmiÅŸ dilimimizi ve yÃ¼klenmiÅŸ rakamÄ± dÃ¶ndÃ¼r
}

() main () {
    slice s = "258";
    int c1 = s~load_digit();
    int c2 = s~load_digit();
    int c3 = s~load_digit();
    ;; burada s "", c1 = 2, c2 = 5, c3 = 8'e eÅŸit
}
```

> ğŸ“Œ **FaydalÄ± baÄŸlantÄ±lar**
>
> `"DeÄŸiÅŸtirme yÃ¶ntemleri" belgelerinde`

---

### Bir sayÄ±yÄ± n. kuvvetine nasÄ±l yÃ¼kseltiriz

```func
;; Optimizasyonsuz variant
int pow (int a, int n) {
    int i = 0;
    int value = a;
    while (i val Ã§ifti ile bir ÅŸey yap

    (key, val, flag) = d.udict_get_next?(256, key);
}
```

> ğŸ“Œ **FaydalÄ± baÄŸlantÄ±lar**
>
> `"SÃ¶zlÃ¼kler primitives" belgelerinde`
>
> `"dict_get_max?()" belgelerinde`
>
> `"dict_get_min?()" belgelerinde`
>
> `"dict_get_next?()" belgelerinde`
>
> `"dict_set()" belgelerinde`

---

### SÃ¶zlÃ¼klerden bir deÄŸeri nasÄ±l sileriz

```func
cell names = new_dict();
names~udict_set(256, 27, "Alice");
names~udict_set(256, 25, "Bob");

names~udict_delete?(256, 27);

(slice val, int key) = names.udict_get?(256, 27);
~dump(val); ;; null() -> bu, anahtarÄ±n sÃ¶zlÃ¼kte bulunmadÄ±ÄŸÄ± anlamÄ±na gelir
```

---

### Cell aÄŸaÃ§larÄ±nÄ± nasÄ±l yineleyerek geÃ§eriz

BildiÄŸimiz gibi, bir `cell`, en fazla `1023 bit` veri ve `4 refs` depolayabilir. Bu sÄ±nÄ±rÄ± aÅŸmak iÃ§in, bir hÃ¼creler aÄŸacÄ± kullanabiliriz; ancak bunu yapabilmek iÃ§in dÃ¼zgÃ¼n veri iÅŸleme saÄŸlamak Ã¼zere yineleyebilmemiz gerekir.

```func
forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";

() main () {
    ;; Ã¶rnek olarak sadece bazÄ± hÃ¼creler
    cell c = begin_cell()
        .store_uint(1, 16)
        .store_ref(begin_cell()
            .store_uint(2, 16)
        .end_cell())
        .store_ref(begin_cell()
            .store_uint(3, 16)
            .store_ref(begin_cell()
                .store_uint(4, 16)
            .end_cell())
            .store_ref(begin_cell()
                .store_uint(5, 16)
            .end_cell())
        .end_cell())
    .end_cell();

    ;; hiÃ§bir verisi olmayan bir tuple oluÅŸturuyoruz, bu yÄ±ÄŸÄ±n rolÃ¼nÃ¼ oynar
    tuple stack = null();
    ;; ana hÃ¼creyi iÅŸlemek iÃ§in yÄ±ÄŸÄ±na gÃ¶tÃ¼r
    stack~push_back(c);
    ;; stack null olmadÄ±ÄŸÄ± sÃ¼rece bunu yap
    while (~ stack.is_null()) {
        ;; yÄ±ÄŸÄ±ndan hÃ¼creyi al ve iÅŸlemek iÃ§in bunu bir dilime dÃ¶nÃ¼ÅŸtÃ¼r
        slice s = stack~pop_back().begin_parse();

        ;; s verisi ile bir ÅŸey yap

        ;; eÄŸer mevcut dilimin herhangi bir referansÄ± varsa, bunlarÄ± yÄ±ÄŸÄ±na ekle
        repeat (s.slice_refs()) {
            stack~push_back(s~load_ref());
        }
    }
}
```

> ğŸ“Œ **FaydalÄ± baÄŸlantÄ±lar**
> 
> `"Lisp tarzÄ± listeler" belgelerinde`
>
> `"null()" belgelerinde`
>
> `"slice_refs()" belgelerinde`

---

### Lisp tarzÄ± listelerde nasÄ±l iterasyon yaparÄ±z

Tuple veri tÃ¼rÃ¼ en fazla 255 deÄŸer tutabilir. EÄŸer bu yeterli deÄŸilse, o zaman bir lisp tarzÄ± liste kullanmalÄ±yÄ±z. Bir tuple'Ä± bir tuple'Ä±n iÃ§ine koyabiliriz, bÃ¶ylece sÄ±nÄ±rÄ± aÅŸarÄ±z.

```func
forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";

() main () {
    ;; bazÄ± Ã¶rnek listeler
    tuple l = null();
    l~push_back(1);
    l~push_back(2);
    l~push_back(3);

    ;; elemanlar arasÄ±nda iterasyon yapÄ±yoruz
    ;; lÃ¼zum aÃ§Ä±sÄ±ndan bu iterasyon ters sÄ±rada
    while (~ l.is_null()) {
        var x = l~pop_back();

        ;; x ile bir ÅŸey yap
    }
}
```

> ğŸ“Œ **FaydalÄ± baÄŸlantÄ±lar**
> 
> `"Lisp tarzÄ± listeler" belgelerinde`
>
> `"null()" belgelerinde`

---

### Deploy mesajÄ± nasÄ±l gÃ¶nderilir (sadece stateInit ile, stateInit ve body ile)

```func
() deploy_with_stateinit(cell message_header, cell state_init) impure {
  var msg = begin_cell()
    .store_slice(begin_parse(msg_header))
    .store_uint(2 + 1, 2) ;; init:(Maybe (Either StateInit ^StateInit))
    .store_uint(0, 1) ;; body:(Either X ^X)
    .store_ref(state_init)
    .end_cell();

  ;; mod 64 - yeni mesajda kalan deÄŸeri taÅŸÄ±mak
  send_raw_message(msg, 64); 
}

() deploy_with_stateinit_body(cell message_header, cell state_init, cell body) impure {
  var msg = begin_cell()
    .store_slice(begin_parse(msg_header))
    .store_uint(2 + 1, 2) ;; init:(Maybe (Either StateInit ^StateInit))
    .store_uint(1, 1) ;; body:(Either X ^X)
    .store_ref(state_init)
    .store_ref(body)
    .end_cell();

  ;; mod 64 - yeni mesajda kalan deÄŸeri taÅŸÄ±mak
  send_raw_message(msg, 64); 
}
```

---

### stateInit hÃ¼cresi nasÄ±l oluÅŸturulur

```func
() build_stateinit(cell init_code, cell init_data) {
  var state_init = begin_cell()
    .store_uint(0, 1) ;; split_depth:(Maybe (## 5))
    .store_uint(0, 1) ;; special:(Maybe TickTock)
    .store_uint(1, 1) ;; (Maybe ^Cell)
    .store_uint(1, 1) ;; (Maybe ^Cell)
    .store_uint(0, 1) ;; (HashmapE 256 SimpleLib)
    .store_ref(init_code)
    .store_ref(init_data)
    .end_cell();
}
```

---

### SÃ¶zleÅŸme adresi nasÄ±l hesaplanÄ±r (stateInit kullanarak)

```func
() calc_address(cell state_init) {
  var future_address = begin_cell() 
    .store_uint(2, 2) ;; addr_std$10
    .store_uint(0, 1) ;; anycast:(Maybe Anycast)
    .store_uint(0, 8) ;; workchain_id:int8
    .store_uint(cell_hash(state_init), 256) ;; address:bits256
    .end_cell();
}
```

---

### AkÄ±llÄ± sÃ¶zleÅŸme mantÄ±ÄŸÄ± nasÄ±l gÃ¼ncellenir

AÅŸaÄŸÄ±da, sayacÄ± artÄ±rma ve akÄ±llÄ± sÃ¶zleÅŸme mantÄ±ÄŸÄ±nÄ± gÃ¼ncelleme iÅŸlevine sahip basit bir `CounterV1` akÄ±llÄ± sÃ¶zleÅŸme Ã¶rneÄŸi bulunmaktadÄ±r.

```func
() recv_internal (slice in_msg_body) {
    int op = in_msg_body~load_uint(32);
    
    if (op == op::increase) {
        int increase_by = in_msg_body~load_uint(32);
        ctx_counter += increase_by;
        save_data();
        return ();
    }

    if (op == op::upgrade) {
        cell code = in_msg_body~load_ref();
        set_code(code);
        return ();
    }
}
```

> ğŸ“Œ **Not:** AkÄ±llÄ± sÃ¶zleÅŸme Ã¼zerinde iÅŸlem yaptÄ±ktan sonra, metrekÃ¼p azaltma Ã¶zelliÄŸinin eksik olduÄŸunu fark edersiniz. AkÄ±llÄ± sÃ¶zleÅŸmenin `CounterV1` kodunu kopyalayÄ±p `increase` iÅŸlevinin yanÄ±na yeni bir `decrease` iÅŸlevi eklemeniz gerekir. ArtÄ±k kodunuz ÅŸu ÅŸekilde gÃ¶rÃ¼nmektedir:

```func
() recv_internal (slice in_msg_body) {
    int op = in_msg_body~load_uint(32);
    
    if (op == op::increase) {
        int increase_by = in_msg_body~load_uint(32);
        ctx_counter += increase_by;
        save_data();
        return ();
    }

    if (op == op::decrease) {
        int decrease_by = in_msg_body~load_uint(32);
        ctx_counter -= decrease_by; ;; burada doÄŸru deÄŸiÅŸken kullanÄ±ldÄ±
        save_data();
        return ();
    }

    if (op == op::upgrade) {
        cell code = in_msg_body~load_ref();
        set_code(code);
        return ();
    }
}
```

`CounterV2` akÄ±llÄ± sÃ¶zleÅŸmesi hazÄ±r olduktan sonra, onu off-chain olarak bir `cell` iÃ§erisine derlemeli ve `CounterV1` akÄ±llÄ± sÃ¶zleÅŸmesine bir gÃ¼ncelleme mesajÄ± gÃ¶ndermelisiniz.

```javascript
await contractV1.sendUpgrade(provider.sender(), {
    code: await compile('ContractV2'),
    value: toNano('0.05'),
});
```

> ğŸ“Œ **FaydalÄ± baÄŸlantÄ±lar**
> 
> `Var olan bir adrese kod yeniden daÄŸÄ±tmak mÃ¼mkÃ¼n mÃ¼ yoksa bu yeni bir sÃ¶zleÅŸme olarak mÄ± daÄŸÄ±tÄ±lmalÄ±dÄ±r?`
>
> `"set_code()" belgelerinde`